#!ipxe

goto ${menu} ||

:debian
set os Debian
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i386 ||
iseq ${os_arch} arm64 && set os_arch arm64 ||
clear debian_version
clear older_release
menu ${os} - ${os_arch}
item --gap Latest Releases
item bullseye ${space} ${os} 11.0 (bullseye)
item buster ${space} ${os} 10.0 (buster)
item stretch ${space} ${os} 9.0 (stretch)
item --gap Testing Releases
item bookworm ${space} ${os} bookworm (testing)
item sid ${space} ${os} sid (unstable)
item --gap Older Releases
item older_release ${space} Set release codename...
choose debian_version || goto debian_exit
iseq ${debian_version} older_release && goto older_release ||
goto mirrorcfg

:mirrorcfg
set debian_mirror ${debian_mirror}
set mirrorcfg mirror/suite=${debian_version}
set dir ${debian_base_dir}/dists/${debian_version}/main/installer-${os_arch}/current/images/netboot/
goto deb_boot_type

:deb_boot_type
menu ${os} [${debian_version}] Installer
item --gap Install types
item text ${space} Text Based Install
item graphical ${space} Graphical Based Install
item rescue ${space} Rescue Mode
item expert ${space} Expert Install
item preseed ${space} Specify preseed url...
choose --default ${type} type || goto debian

echo ${cls}
goto deb_${type}

:deb_rescue
set install_params rescue/enable=true
goto deb_text

:deb_expert
set install_params priority=low
goto deb_text

:deb_preseed
echo -n Specify preseed URL for ${os} ${debian_version}: && read preseedurl
set install_params auto=true priority=critical preseed/url=${preseedurl}
goto deb_text

:deb_text
set dir ${dir}${menu}-installer/${os_arch}
goto deb_boot

:deb_graphical
set dir ${dir}gtk/${menu}-installer/${os_arch}
set install_params vga=788
goto deb_boot

:deb_boot
imgfree
kernel ${debian_mirror}/${dir}/linux ${install_params} ${netcfg} ${mirrorcfg} initrd=initrd.magic ${cmdline}
initrd ${debian_mirror}/${dir}/initrd.gz
echo
echo MD5sums:
md5sum linux initrd.gz
boot

:debian_exit
clear menu
exit 0

:proxmox
clear proxmox_choice
clear proxmox_version
set os Proxmox
menu ${os}
item --gap ${os} VE
item pve-normal ${space} ${os} VE 7.1-2
item pve-debug ${space} ${os} VE 7.1-2 (Debug)
choose proxmox_choice || goto proxmox_exit
goto ${proxmox_choice}

:pve-normal
set params splash=verbose
goto boot-pve

:pve-debug
set params splash=verbose proxdebug
goto boot-pve

:boot-pve
set kernel_url ${live_endpoint}/asset-mirror/releases/download/7.1-2-a42c4ce8/
set proxmox_version 7.1-2
imgfree
kernel ${kernel_url}vmlinuz vga=normal video=vesafb:ywrap,mtrr ramdisk_size=16777216 rw quiet ${params} initrd=initrd.magic console=tty0 console=ttyS0,115200n8 nomodeset nofb modprobe.blacklist=megaraid_sas,mpt3sas,nvidiafb ${cmdline}
initrd ${kernel_url}initrd
initrd http://192.168.28.7/asset-mirror/releases/download/7.1-2-a42c4ce8/proxmox.iso /proxmox.iso
boot

:proxmox_exit
clear menu
exit 0
